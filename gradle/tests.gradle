/*******************************************************************************
 * The John Operating System Project is the collection of software and configurations
 * to generate IoT EcoSystem, like the John Operating System Platform one.
 * Copyright (C) 2021 Roberto Pompermaier
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 ******************************************************************************/

/**
 * Gradle configurations to manage JOSP project's tests.
 *
 * Summary:
 * - SourceSes configs
 * - Tests Tasks
 * - IntelliJ Modules
 *
 * This file configure and provide test tasks for all JOSP project's components:
 * - JOSP Commons: communication tests
 * - JOD: communication and structure tests
 * - JSL: communication tests
 *
 * Component's test are organized by one sourceSets for each unit of the component
 * (comm, struct, perms...) and this file provide specific and generic (All) tests
 * tasks.
 */


// -----------------
// SourceSes configs
// -----------------

sourceSets {

    // Test's Commons

    test_Commons {
        java.srcDirs 'tests/test_Commons/java'
        resources.srcDirs 'tests/test_Commons/resources'
    }


    // JOD

    test_jospJODStruct {
        java.srcDirs 'tests/test_jospJODStruct/java'
        resources.srcDirs 'tests/test_jospJODStruct/resources'
    }

    test_jospJODComm {
        java.srcDirs 'tests/test_jospJODComm/java'
        resources.srcDirs 'tests/test_jospJODComm/resources'
    }

    test_jospJODExec {
        java.srcDirs 'tests/test_jospJODExec/java'
        resources.srcDirs 'tests/test_jospJODExec/resources'
    }

}

dependencies {
    // Test's commons
    test_CommonsImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    test_CommonsRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    test_CommonsImplementation "com.robypomper.josp:jospJSL:${project.ext.get("version.deps.jospCommons")}"
    test_CommonsImplementation sourceSets.main.output
    test_CommonsImplementation sourceSets.main.runtimeClasspath


    // JOD
    test_jospJODStructImplementation sourceSets.test_Commons.output
    test_jospJODStructImplementation sourceSets.test_Commons.runtimeClasspath

    test_jospJODCommImplementation sourceSets.test_Commons.output
    test_jospJODCommImplementation sourceSets.test_Commons.runtimeClasspath

    test_jospJODExecImplementation sourceSets.test_Commons.output
    test_jospJODExecImplementation sourceSets.test_Commons.runtimeClasspath
}


// -----------
// Tests Tasks
// -----------

task jospJODAllTest(type: Test) {
    description = 'Runs all JOD tests.'
    group = 'verification'
}
check.dependsOn jospJODAllTest

task jospJODStructTest(type: Test) {
    description = 'Runs JOD Communication tests.'
    group = 'verification'

    testClassesDirs = sourceSets.test_jospJODStruct.output.classesDirs
    classpath = sourceSets.test_jospJODStruct.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
    //jvmArgs '-Djavax.net.debug=all'
}
jospJODAllTest.dependsOn jospJODStructTest

task jospJODCommTest(type: Test) {
    description = 'Runs JOD Communication tests.'
    group = 'verification'

    testClassesDirs = sourceSets.test_jospJODComm.output.classesDirs
    classpath = sourceSets.test_jospJODComm.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
    //jvmArgs '-Djavax.net.debug=all'
}
jospJODAllTest.dependsOn jospJODCommTest

task jospJODExecTest(type: Test) {
    description = 'Runs JOD Executors tests.'
    group = 'verification'

    testClassesDirs = sourceSets.test_jospJODExec.output.classesDirs
    classpath = sourceSets.test_jospJODExec.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
    //jvmArgs '-Djavax.net.debug=all'
}
jospJODAllTest.dependsOn jospJODExecTest


// ----------------
// IntelliJ Modules
// ----------------

// gradle > intellij settings
idea {
    module {
        testSourceDirs += project.sourceSets.test_jospJODStruct.java.srcDirs
        testSourceDirs += project.sourceSets.test_jospJODStruct.resources.srcDirs
        testSourceDirs += project.sourceSets.test_jospJODComm.java.srcDirs
        testSourceDirs += project.sourceSets.test_jospJODComm.resources.srcDirs
        testSourceDirs += project.sourceSets.test_jospJODExec.java.srcDirs
        testSourceDirs += project.sourceSets.test_jospJODExec.resources.srcDirs
    }
}